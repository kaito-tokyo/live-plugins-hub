# Maintainer: Kaito Udagawa <umireon at kaito dot tokyo>
pkgname=live-backgroundremoval-lite
pkgver=3.5.1
pkgrel=1
pkgdesc='Live Background Removal Lite for OBS Studio'
arch=('x86_64')
url="https://github.com/kaito-tokyo/${pkgname}"
license=('GPL-3.0-or-later')
depends=('curl' 'fmt' 'ncnn' 'obs-studio')
makedepends=('cmake' 'ninja')
conflicts=("${pkgname}-git" "${pkgname}-git-debug")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kaito-tokyo/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('01819ee4609bbcd00c7ca2c963694e479a7c32543fd517914c6627b65d606fb0')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_PKGCONFIG=ON \
    -DBUILD_TESTING=OFF \
    -GNinja
  cmake --build build
}

package() {
  cd "${pkgname}-${pkgver}"
  DESTDIR="$pkgdir" cmake --install build
}
