# Maintainer: Kaito Udagawa <umireon at kaito dot tokyo>
pkgname=live-backgroundremoval-lite-git
_pkgname="${pkgname%-git}"
pkgver=3.1.1.r2.gf69f5b6
pkgrel=1
pkgdesc='Live Background Removal Lite for OBS Studio'
arch=('x86_64')
url="https://github.com/kaito-tokyo/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('curl' 'fmt' 'ncnn' 'obs-studio')
makedepends=('cmake' 'git' 'ninja')
provides=("${_pkgname}" "${_pkgname}-debug")
conflicts=("${_pkgname}" "${_pkgname}-debug")
source=("${_pkgname}::git+https://github.com/kaito-tokyo/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}


build() {
  cd "${_pkgname}"
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_PKGCONFIG=ON \
    -DBUILD_TESTING=OFF \
    -GNinja
  cmake --build build
}

package() {
  cd "${_pkgname}"
  DESTDIR="$pkgdir" cmake --install build
}
