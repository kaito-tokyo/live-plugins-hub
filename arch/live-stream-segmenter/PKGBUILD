# Maintainer: Kaito Udagawa <umireon at kaito dot tokyo>
pkgname=live-stream-segmenter
pkgver=1.0.3
pkgrel=1
pkgdesc='Live Stream Segmenter for OBS Studio'
arch=('x86_64')
url="https://github.com/kaito-tokyo/${pkgname}"
license=('GPL-3.0-or-later')
depends=('curl' 'fmt' 'nlohmann-json' 'obs-studio' 'qcoro' 'qtkeychain-qt6')
makedepends=('cmake' 'ninja')
conflicts=("${pkgname}-git" "${pkgname}-git-debug")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kaito-tokyo/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('aac3d8a32b8362e3b2d0c4c79918a7bab259a832ad6182a47dc36815bd4ce736')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -GNinja
  cmake --build build
}

package() {
  cd "${pkgname}-${pkgver}"
  DESTDIR="$pkgdir" cmake --install build
}
