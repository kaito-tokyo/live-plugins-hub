# Maintainer: Kaito Udagawa <umireon at kaito dot tokyo>
pkgname=live-stream-segmenter
pkgver=0.1.5
pkgrel=1
pkgdesc='Live Stream Segmenter for OBS Studio'
arch=('x86_64')
url="https://github.com/kaito-tokyo/${pkgname}"
license=('GPL-3.0-or-later')
depends=('curl' 'fmt' 'nlohmann-json' 'obs-studio')
makedepends=('cmake' 'ninja')
conflicts=("${pkgname}-git" "${pkgname}-git-debug")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kaito-tokyo/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('62951cc735a61204be5b8f792f3a3edd6360708a54a721c487eefbff69820c49')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_PKGCONFIG=ON \
    -DBUILD_TESTING=OFF \
    -GNinja
  cmake --build build
}

package() {
  cd "${pkgname}-${pkgver}"
  DESTDIR="$pkgdir" cmake --install build
}
