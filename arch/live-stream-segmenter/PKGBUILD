# Maintainer: Kaito Udagawa <umireon at kaito dot tokyo>
pkgname=live-stream-segmenter
pkgver=1.0.4
pkgrel=1
pkgdesc='Live Stream Segmenter for OBS Studio'
arch=('x86_64')
url="https://github.com/kaito-tokyo/${pkgname}"
license=('GPL-3.0-or-later')
depends=('curl' 'fmt' 'nlohmann-json' 'obs-studio' 'qcoro' 'qtkeychain-qt6')
makedepends=('cmake' 'ninja')
conflicts=("${pkgname}-git" "${pkgname}-git-debug")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kaito-tokyo/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('10e92965bd9caa571fd508f1b575ac778b002e0d0377e87fbca87cfa7fc39e4b')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -GNinja
  cmake --build build
}

package() {
  cd "${pkgname}-${pkgver}"
  DESTDIR="$pkgdir" cmake --install build
}
