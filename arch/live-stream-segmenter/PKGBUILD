# Maintainer: Kaito Udagawa <umireon at kaito dot tokyo>
pkgname=live-stream-segmenter
pkgver=0.1.4
pkgrel=1
pkgdesc='Live Stream Segmenter for OBS Studio'
arch=('x86_64')
url="https://github.com/kaito-tokyo/${pkgname}"
license=('GPL-3.0-or-later')
depends=('cpp-httplib' 'curl' 'fmt' 'nlohmann-json' 'obs-studio')
makedepends=('cmake' 'ninja')
conflicts=("${pkgname}-git" "${pkgname}-git-debug")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kaito-tokyo/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('0bb06c3f3284bd82664dc2168a566df2d099b1af0afe874edac626da9acd0aab')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_PKGCONFIG=ON \
    -GNinja
  cmake --build build
}

package() {
  cd "${pkgname}-${pkgver}"
  DESTDIR="$pkgdir" cmake --install build
}
